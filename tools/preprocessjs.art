keywordPredicates: []
keywordRest: []

loop symbols [k,v][
    switch suffix? k "?" -> 'keywordPredicates ++ chop to :string k
                         -> 'keywordRest ++ to :string k
]

keywordPredicates: sort keywordPredicates
keywordRest: sort keywordRest -- ["keywordPredicates" "keywordRest"]

changes: #[
    "src/theme/resources/scripts/docsearch.js": [
        'basePath
    ]
    "src/theme/resources/scripts/playground.js": [
        'basePath
    ]
    "src/theme/resources/scripts/mode-arturo.js": [
        'keywordPredicates
        'keywordRest
    ]
    "src/theme/resources/scripts/main.js": [
        'keywordPredicates
        'keywordRest
    ]
]

params: #[
    basePath: arg\0
    keywordPredicates: keywordPredicates
    keywordRest: keywordRest
]

loop changes [file, replacements][
    print ["Processing:" file]
    content: << file
    loop replacements 'rep [
        print ["- replacing:" rep]
        content: replace content "%<[" ++ (to :string rep) ++ "]>%"
                                 params\[rep]
    ]
    content >> file
]

print "end--"